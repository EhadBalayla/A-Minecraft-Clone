#version 330 core
layout (location = 0) in uvec3 vert;
layout (location = 1) in uint tex;
layout (location = 2) in uint ext;

out vec2 TexCoords;

uniform mat4 view;
uniform mat4 projection;
uniform mat4 model;

uniform uint LODFactor;

uvec3 decodeOffset(uint extra);
vec2 decodeUVoffset(uint extra);

void main() {
	uint texIndex = tex;
	uint extras = ext;
	
	uvec3 posOffset = decodeOffset(extras);

	gl_Position = projection * view * model * vec4(vert + posOffset * LODFactor, 1.0);
	
	uint indexX = texIndex % uint(16);
	uint indexY = texIndex / uint(16);
	float uvX =  float(indexX) / 16;
	float uvY = float(indexY) / 16;
	vec2 uvOffset = decodeUVoffset(extras);
	
	TexCoords = vec2(uvX, uvY) + uvOffset;
}

uvec3 decodeOffset(uint extra) {
	uint number = (extra >> 2) & 0x7u; //gets the block corner number
	const uvec3 offsets[8] = uvec3[8](
        uvec3(0,0,0), uvec3(1,0,0), uvec3(1,0,1), uvec3(0,0,1),
        uvec3(0,1,0), uvec3(1,1,0), uvec3(1,1,1), uvec3(0,1,1)
    );
	return offsets[number];
}
vec2 decodeUVoffset(uint extra) {
	uint number = extra & 0x3u;
	const vec2 offsets[4] = vec2[4](
		vec2(0, 0), vec2(0.0625, 0), vec2(0.0625, 0.0625), vec2(0, 0.0625)
	);
	return offsets[number];
}